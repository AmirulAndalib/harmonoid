import java.io.File
import java.nio.file.Files
import java.security.MessageDigest

group 'com.alexmercerind.media_kit_libs_android_audio'
version '1.0'

buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.0'
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    namespace 'com.alexmercerind.media_kit_libs_android_audio'

    compileSdkVersion 31

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 16
    }

    dependencies {
        implementation fileTree(dir: "$buildDir/output", include: "*.jar")
    }
}


task downloadDependencies(type: Exec)  {
    
        def outputDir = file("$buildDir/output")
        if (outputDir.exists()) {
            outputDir.deleteDir()
        }
        outputDir.mkdirs()

        // Download all *.jar dependencies & verify their MD5 checksums.
        def filesToDownload = [
            ["url": "https://github.com/media-kit/libmpv-android-audio-build/releases/download/v1.1.7/default-arm64-v8a.jar", "md5": "d6e23e045502143f33ff083824587b08", "destination": file("$buildDir/v1.1.7/default-arm64-v8a.jar")],
            ["url": "https://github.com/media-kit/libmpv-android-audio-build/releases/download/v1.1.7/default-armeabi-v7a.jar", "md5": "df2a9de7df8324d95b0d65eb6dfc3c5f", "destination": file("$buildDir/v1.1.7/default-armeabi-v7a.jar")],
            ["url": "https://github.com/media-kit/libmpv-android-audio-build/releases/download/v1.1.7/default-x86_64.jar", "md5": "262cc5fa9c1537488f1820c971b12c51", "destination": file("$buildDir/v1.1.7/default-x86_64.jar")],
            ["url": "https://github.com/media-kit/libmpv-android-audio-build/releases/download/v1.1.7/default-x86.jar", "md5": "208372c28739346c217aa7c10a4064c9", "destination": file("$buildDir/v1.1.7/default-x86.jar")]
        ]

        filesToDownload.each { fileInfo ->
            def destFile = fileInfo.destination

            if (destFile.exists()) {
                def calculatedMD5 = MessageDigest.getInstance("MD5").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()

                if (calculatedMD5 != fileInfo.md5) {
                    destFile.delete()
                    println "MD5 mismatch. File deleted: ${destFile}"
                }

            }

            if (!destFile.exists()) {
                destFile.parentFile.mkdirs()
                println "Downloading file from: ${fileInfo.url}"
                destFile.withOutputStream { os ->
                    os << new URL(fileInfo.url).openStream()
                }
                def calculatedMD5 = MessageDigest.getInstance("MD5").digest(Files.readAllBytes(destFile.toPath())).encodeHex().toString()
                if (calculatedMD5 != fileInfo.md5) {
                    throw new GradleException("MD5 verification failed for ${destFile}")
                }
               
            }

            copy {
                from destFile
                into outputDir
            }
        }
    
}

assemble.dependsOn(downloadDependencies)
